buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'

    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
    }

    // Used to customize the name of generated AAR file.
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "GodotOneStore.${variant.name}.aar"
        }
    }

}

dependencies {
    implementation 'androidx.annotation:annotation-jvm:1.7.0'

    /*
    Used to provide dependency on the Godot Android library. A version of that
    library will be made available for each stable release of Godot.
    `compileOnly` is used instead of `implementation` to ensure that the
     godot library is not bundled with the generated plugin AAR file. This is
     necessary since the Godot editor will also provide a version of the godot
     library when building the final binary.
     */

    compileOnly fileTree(dir: 'libs', include: ['godot-lib*.aar','TDAnalytics.aar','TDCore.aar'])
    implementation 'cn.thinkingdata.android:ThinkingAnalyticsSDK:2.8.2'
}

group = 'com.slabgames.godot'
version = '0.1.0'
ext {
    artifact = 'godot-onestore'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

afterEvaluate {
//    generateReleaseBuildConfig.enabled = false
//    generateDebugBuildConfig.enabled = false

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.release
                groupId group
                artifactId artifact
                version version
            }
        }
    }

    // Bintray
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        publications = ['MyPublication']
        pkg {
            repo = 'maven'
            name = artifact
            userOrg = 'slab'
            desc = 'Onestore plugin for godot engine.'
            websiteUrl = 'https://github.com/slabgames/godot-onestore'
            vcsUrl = 'https://github.com/slabgames/godot-onestore.git'
            licenses = ["MIT", "BSD"]
            publish = true
            publicDownloadNumbers = true
            version {
            }
        }
    }
}